@namespace: Parseidon.Parser;
@class: ParseidonParser;
@rootnode: Grammar;

# Grammar
Grammar             = _(Option_)* Definition+_;

Option              = '@' ValuePair_LineEnd;

# Rule structure
Definition          = _RuleOptions Identifier_-Equal_Expression Properties? LineEnd;
:RuleOptions        = UseRuleNameAsError? (IsTerminal / Drop)? TreatInline?;
Expression          = Sequence (Or_Sequence)*;
Sequence            = Prefix+;
Prefix              = Drop? Suffix;
Suffix              = Primary (Optional / ZeroOrMore / OneOrMore)?_;
:Primary            = TreatInline? Identifier
                    / Literal
                    / -BracketOpen_Expression_-BracketClose
                    / Regex
                    / AllChar;  
:Properties         = -CurlyBracketOpen_ValuePair_(','_ValuePair_)* -CurlyBracketClose_;

# Values
ValuePair           = Identifier_(-ValueAssign_Value_)?;
:ValueAssign        = Colon;
:Value              = Literal / Number / Boolean / CSharpIdentifier;
^$Number            = ('0' / ([1-9] [0-9]*));
$Boolean            = (True / False);
:True               = 'true';
:False              = 'false';
^$CSharpIdentifier  = IdentStart CSharpIdentCont {ErrorName: 'C#-Identifier'};
CSharpIdentCont     = [a-zA-Z0-9.]*;

# Identifier
^$Identifier        = Underscore / (IdentStart IdentCont);
IdentStart          = [a-zA-Z];
IdentCont           = [a-zA-Z0-9]*;

# Literal
^$Literal           = (SingleQuotedLiteral / DoubleQuotedLiteral);
:SingleQuotedLiteral= (-'\'' Char*  -'\'');
:DoubleQuotedLiteral= (-'"' Char* -'"');
Char                = [^'\\] / '\\\'' / '\\\\' / EscapeChars / UnicodeChars;
EscapeChars         = '\\' [\\0abfnrtv"'?];

# Regular Expression
Regex               = CharacterClass Quantifier?;
$CharacterClass     = SquareBracketOpen Negation? ClassContent+ SquareBracketClose;
:Negation           = '^';
ClassContent        = CharRange / PredefinedClass / EscapedChar / SingleChar;
:CharRange          = SingleChar '-' SingleChar;
SingleChar          = [^\\\[\]\-] / UnicodeChars;
EscapedChar         = '\\' [.\\\[\]\-\^];
PredefinedClass     = '\\d' / '\\D' / '\\w' / '\\W' / '\\s' / '\\S' / '\\t' / '\\r' / '\\n' / '\\v' / '\\f';
:Quantifier         = CurlyBracketOpen Number CurlyBracketClose;

# All characters
AllChar             = Dot;

# Operator
Or                  = '/';

# Quantifier
Optional            = '?';
ZeroOrMore          = '*';
OneOrMore           = '+';

# Rule markers
Drop                = '-';
IsTerminal          = '$' DontEscapeOutput?;
:DontEscapeOutput   = '$';
TreatInline         = ':';
UseRuleNameAsError  = '^';

$$UnicodeChars      = '\\u' [0-9a-fA-F]{4};

# Grammar structure
^-:LineEnd          = ';';
-_                  = (NewLine / WhiteSpace / Comment)*;
^WhiteSpace         = [ \t];
^NewLine            = '\r\n' / [\r\n];
Comment             = '#' [^\r\n]*;

# Shared characters
:Equal              = '=';
:Dot                = '.';
:Colon              = ':';
:BracketOpen        = '(';
:BracketClose       = ')';
:CurlyBracketOpen   = '{';
:CurlyBracketClose  = '}';
:SquareBracketOpen  = '[';
:SquareBracketClose = ']';
:Underscore         = '_';

