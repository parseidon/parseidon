@namespace Parseidon.Parser;
@class ParseidonParser;
@rootnode Grammar;

Grammar             = Config Definition+;
Config              = Namespace ClassName RootNode;
Namespace           = Spacing -'@namespace' Spacing CSharpIdentifier LineEnd;
ClassName           = Spacing -'@class' Spacing CSharpIdentifier LineEnd;
RootNode            = Spacing -'@rootnode' Spacing Identifier LineEnd;
Definition          = Spacing RuleOptions Identifier -Equal Expression LineEnd;
:RuleOptions        = PromoteErrorName? (IsTerminal / Drop / TreatInline)?;
Expression          = Sequence (Or Sequence)*;
Sequence            = Prefix*;
Prefix              = Drop? Suffix;
Suffix              = Primary (Optional / ZeroOrMore / OneOrMore)?;
Primary             = Identifier
                    / Literal
                    / -BracketOpen Expression -BracketClose
                    / Regex
                    / Dot;  
^$Identifier         = IdentStart IdentCont Spacing;
IdentStart          = [a-zA-Z_];
IdentCont           = [a-zA-Z0-9_]*;
^$CSharpIdentifier   = IdentStart CSharpIdentCont Spacing;
CSharpIdentCont     = [a-zA-Z0-9_.]*;
^$Literal            = ((-'\'' Char*  -'\'') / ('"' Char* '"')) Spacing ;
Char                = [^'\\] / '\\\'' / '\\\\' / EscapeChars / UnicodeChars;
Regex               = RegularExpression Quantifier? Spacing;
$RegularExpression  = '[' BracketContent+ ']';
BracketContent      = ('\\' [.\\\[\]-^]) / [^\[\\\]] / EscapeChars / UnicodeChars;
:Quantifier         = '{' Number '}';

Or                  = '/' Spacing;
Optional            = '?' Spacing;
ZeroOrMore          = '*' Spacing;
OneOrMore           = '+' Spacing;
Drop                = '-' Spacing;
IsTerminal          = '$' '$'? Spacing;
TreatInline         = ':' Spacing;
PromoteErrorName    = '^' Spacing;

EscapeChars         = '\\' [\\0abfnrtv"'?];
$$UnicodeChars        = '\\u' [0-9a-fA-F]{4};

Equal               = '=' Spacing;
^-LineEnd            = Spacing ';';
Dot                 = '.' Spacing;
-Spacing            = (NewLine / WhiteSpace / Comment)*;
^WhiteSpace          = [ \t\r\n];
^NewLine             = [\r];
^$Number             = '0' / ([1-9] [0-9]*);
Comment             = '#' [^\r\n]* [\r\n];
BracketOpen         = '(' Spacing;
BracketClose        = ')' Spacing;
