@namespace: Parseidon.Parser;
@class: ParseidonParser;
@rootnode: Grammar;

# Grammar
Grammar             = Spacing (Option Spacing)* Definition+ Spacing;

Option              = '@' ValuePair LineEnd;

# Rule structure
Definition          = Spacing RuleOptions Identifier Spacing -Equal Spacing Expression Properties? LineEnd;
:RuleOptions        = UseRuleNameAsError? (IsTerminal / Drop)? TreatInline?;
Expression          = Sequence (Or Spacing Sequence)*;
Sequence            = Prefix+;
Prefix              = Drop? Suffix;
Suffix              = Primary (Optional / ZeroOrMore / OneOrMore)? Spacing;
:Primary            = TreatInline? Identifier
                    / Literal
                    / -BracketOpen Spacing Expression -BracketClose
                    / Regex
                    / AllChar;  
:Properties         = -CurlyBracketOpen Spacing ValuePair (',' Spacing ValuePair)* -CurlyBracketClose Spacing;

# Values
ValuePair           = Identifier Spacing (-ValueAssign Value Spacing)?;
:ValueAssign        = ':' Spacing;
:Value              = Literal / Number / Boolean / CSharpIdentifier;
^$Number            = ('0' / ([1-9] [0-9]*));
$Boolean            = (True / False);
:True               = 'true';
:False              = 'false';
^$CSharpIdentifier  = IdentStart CSharpIdentCont {ErrorName: 'C#-Identifier'};
CSharpIdentCont     = [a-zA-Z0-9_.]*;

# Identifier
^$Identifier        = IdentStart IdentCont;
IdentStart          = [a-zA-Z_];
IdentCont           = [a-zA-Z0-9_]*;

# Literal
^$Literal           = (SingleQuotedLiteral / DoubleQuotedLiteral);
:SingleQuotedLiteral= (-'\'' Char*  -'\'');
:DoubleQuotedLiteral= (-'"' Char* -'"');
Char                = [^'\\] / '\\\'' / '\\\\' / EscapeChars / UnicodeChars;
EscapeChars         = '\\' [\\0abfnrtv"'?];

# Regular Expression
Regex               = CharacterClass Quantifier?;
$CharacterClass     = SquareBracketOpen Negation? ClassContent+ SquareBracketClose;
:Negation           = '^';
ClassContent        = CharRange / PredefinedClass / EscapedChar / SingleChar;
:CharRange          = SingleChar '-' SingleChar;
SingleChar          = [^\\\[\]\-] / UnicodeChars;
EscapedChar         = '\\' [.\\\[\]\-\^];
PredefinedClass     = '\\d' / '\\D' / '\\w' / '\\W' / '\\s' / '\\S' / '\\t' / '\\r' / '\\n' / '\\v' / '\\f';
:Quantifier         = CurlyBracketOpen Spacing Number CurlyBracketClose;

# All characters
AllChar             = Dot;

# Operator
Or                  = '/';

# Quantifier
Optional            = '?';
ZeroOrMore          = '*';
OneOrMore           = '+';

# Rule markers
Drop                = '-';
IsTerminal          = '$' DontEscapeOutput?;
:DontEscapeOutput   = '$';
TreatInline         = ':';
UseRuleNameAsError  = '^';

$$UnicodeChars      = '\\u' [0-9a-fA-F]{4};

# Grammar structure
^-:LineEnd          = ';';
-Spacing            = (NewLine / WhiteSpace / Comment)*;
^WhiteSpace         = [ \t];
^NewLine            = '\r\n' / [\r\n];
Comment             = '#' [^\r\n]*;

# Shared characters
:Equal              = '=';
:Dot                = '.';
:BracketOpen        = '(';
:BracketClose       = ')';
:CurlyBracketOpen   = '{';
:CurlyBracketClose  = '}';
:SquareBracketOpen  = '[';
:SquareBracketClose = ']';

