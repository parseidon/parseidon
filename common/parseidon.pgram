@namespace: Parseidon.Parser;
@class: ParseidonParser;
@rootnode: Grammar;

Grammar             = Option* Definition+ Spacing;
Option              = Spacing '@' ValuePair LineEnd;
ClassName           = Spacing -'@class' Spacing CSharpIdentifier LineEnd;
Definition          = Spacing RuleOptions Identifier -Equal Expression AdditionalProperties? LineEnd;
:RuleOptions        = UseRuleNameAsError? (IsTerminal / Drop / TreatInline)?;

:AdditionalProperties= -'{' Spacing ValuePair (',' Spacing ValuePair)* -'}';
ValuePair           = Identifier -ValueAssign Value;
:ValueAssign        = ':' Spacing;
:Value              = Literal / Number / Boolean / CSharpIdentifier;

Expression          = Sequence (Or Sequence)*;
Sequence            = Prefix+;
Prefix              = Drop? Suffix;
Suffix              = Primary (Optional / ZeroOrMore / OneOrMore)?;
:Primary             = Identifier
                    / Literal
                    / -BracketOpen Expression -BracketClose
                    / Regex
                    / Dot;  
^$Identifier        = IdentStart IdentCont Spacing;
IdentStart          = [a-zA-Z_];
IdentCont           = [a-zA-Z0-9_]*;
^$CSharpIdentifier  = IdentStart CSharpIdentCont Spacing {ErrorName: 'C#-Identifier'};
CSharpIdentCont     = [a-zA-Z0-9_.]*;
^$Literal           = (SingleQuotedLiteral / DoubleQuotedLiteral) Spacing ;
:SingleQuotedLiteral= (-'\'' Char*  -'\'');
:DoubleQuotedLiteral= (-'"' Char* -'"');
Char                = [^'\\] / '\\\'' / '\\\\' / EscapeChars / UnicodeChars;
Regex               = CharacterClass Quantifier? Spacing;
$CharacterClass     = '[' Negation? ClassContent+ ']';
:Negation           = '^';
ClassContent        = CharRange / PredefinedClass / EscapedChar / SingleChar;
:CharRange          = SingleChar '-' SingleChar;
SingleChar          = [^\\\[\]\-] / UnicodeChars;
EscapedChar         = '\\' [.\\\[\]\-\^];
PredefinedClass     = '\\d' / '\\D' / '\\w' / '\\W' / '\\s' / '\\S' / '\\t' / '\\r' / '\\n' / '\\v' / '\\f';
:Quantifier         = '{' Number '}';

Or                  = '/' Spacing;
Optional            = '?' Spacing;
ZeroOrMore          = '*' Spacing;
OneOrMore           = '+' Spacing;
Drop                = '-' Spacing;
IsTerminal          = '$' DontEscapeOutput? Spacing;
:DontEscapeOutput    = '$';
TreatInline         = ':' Spacing;
UseRuleNameAsError  = '^' Spacing;

EscapeChars         = '\\' [\\0abfnrtv"'?];
$$UnicodeChars      = '\\u' [0-9a-fA-F]{4};

Equal               = '=' Spacing;
^-LineEnd           = Spacing ';';
Dot                 = '.' Spacing;
-Spacing            = (NewLine / WhiteSpace / Comment)*;
^WhiteSpace         = [ \t];
^NewLine            = '\r\n' / [\r\n];
^$Number            = ('0' / ([1-9] [0-9]*)) Spacing;
Comment             = '#' [^\r\n]*;
BracketOpen         = '(' Spacing;
BracketClose        = ')' Spacing;
$Boolean            = (True / False) Spacing;
:True               = 'true';
:False              = 'false';
